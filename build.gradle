buildscript {
	apply from: './libs/util/deps.gradle'

	repositories {
		google()
		jcenter()
		maven { url 'https://maven.fabric.io/public' }
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
		classpath 'com.android.tools.build:gradle:3.4.2'
		classpath 'com.google.gms:google-services:4.3.0'
		classpath 'io.fabric.tools:gradle:1.26.1'
	}
}

allprojects {
	repositories {
		google()
		jcenter()

		flatDir { dirs new File(rootProject.projectDir, 'libs') }

		maven { url 'https://jitpack.io' }
	}
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
	}
}

android {
	compileSdkVersion Versions.App.compileSdk
	defaultConfig {
		minSdkVersion Versions.App.minSdk
		targetSdkVersion Versions.App.targetSdk

		applicationId 'ir.hco.appian.app'
		versionCode 101
		versionName '01.01'

		resConfigs 'fa'
		vectorDrawables.useSupportLibrary true

		buildConfigField 'String', 'APP_LINK', '"bit.ly/hossainco"'
		buildConfigField 'String', 'DEVELOPER_ID', '"HossainCo"'
		buildConfigField 'String', 'DEVELOPER_TITLE', '"HossainCo"'
		buildConfigField 'String', 'DEVELOPER_EMAIL', '"hco@post.com"'
		buildConfigField 'String', 'DEVELOPER_PHONE', '"+98 933 481 5552"'
		buildConfigField 'String', 'DEVELOPER_LI', '"https://www.linkedin.com/in/hossain-khademian-45692166"'
		buildConfigField 'String', 'DEVELOPER_TW', '"https://twitter.com/HKhademian"'
	}
	buildTypes {
		debug {
			versionNameSuffix '-debug'
		}
		release {
			minifyEnabled true
			shrinkResources true
			// useProguard false
			signingConfig signingConfigs.debug
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
				'proguard-rules.pro',
				'libs/util/proguard-flatten.pro',
				'libs/util/proguard-dictionary.pro'
		}
	}

	flavorDimensions 'product', 'publisher'

	productFlavors {
		foroog {
			dimension 'product'
			applicationId 'ir.poemian.foroog'
			versionCode 108
			versionName '01.08'

			ext {
				googlePlay = {
					buildConfigField 'String', 'APP_LINK', '"bit.ly/2LZbfcv"'
				}
				bazaar = {
					buildConfigField 'String', 'APP_LINK', '"bit.ly/31fI8oN"'
					buildConfigField 'String', 'ADAD_APP_TOKEN', '"b7ad94119fb04040b4fffa9a944a53e5"'
					buildConfigField 'String', 'ADAD_BANNER_TOKEN', '"b876c006-b9f3-416e-9739-baed0ba184c8"'
					buildConfigField 'String', 'ADAD_CONTENT_TOKEN', '"5a5eba4c-8507-40a1-a3ad-1fda5755883f"'
					buildConfigField 'String', 'ADAD_FULL_TOKEN', '"0214FF2C-3CDF-4DCE-B2B2-7F2CE308C166"'
					buildConfigField 'String', 'ADAD_NOTIFICATION_TOKEN', '"0214FF2C-3CDF-4DCE-B2B2-7F2CE308C166"'
				}
				myket = {
					buildConfigField 'String', 'APP_LINK', '"bit.ly/3366uDn"'
					buildConfigField 'String', 'MAGNET_BANNER_TOKEN', '"d6605e6f1cc208d7a10b6ea581741f6b"'
					buildConfigField 'String', 'MAGNET_CONTENT_TOKEN', '"c3e3afb71cc208d7bfd536c94e260981"'
					buildConfigField 'String', 'MAGNET_FULL_TOKEN', '"e546c86e1cc208d79bf33685d973aad9"'
				}
			}
		}
	}

	productFlavors {
		googlePlay {
			dimension 'publisher'
			versionNameSuffix '-play'
			buildConfigField 'String', 'DEVELOPER_ID', '"HossainCo"'
		}
		bazaar {
			dimension 'publisher'
			versionNameSuffix '-bazaar'
			buildConfigField 'String', 'DEVELOPER_ID', '"hossainco"'
		}
		myket {
			dimension 'publisher'
			versionNameSuffix '-myket'
			buildConfigField 'String', 'DEVELOPER_ID', '"dev-19620"'
		}
	}

	process(applicationVariants)

	packagingOptions {
		exclude '/kotlin_metadata/**'
		exclude '/kotlin/**'
		exclude '/fabric/**'
		exclude '/*.properties'

		exclude '/PufferDB.proto'
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${Versions.kotlin}"
	// implementation Deps.coroutines
	implementation Deps.anko_sdk27

	implementation Deps.core_ktx
	implementation Deps.fragment
	implementation Deps.fragment_ktx
	implementation Deps.vectordrawable
	implementation Deps.recyclerview
	implementation Deps.cardview

	implementation Deps.lifecycle_extensions
	implementation Deps.lifecycle_viewmodel_ktx
	kapt Deps.lifecycle_compiler

	implementation project(':lib-util')
	implementation project(':lib-firebase')
	implementation project(':lib-fcm')
	implementation project(':lib-pushe')

	googlePlayImplementation project(':lib-appbrain')
	bazaarImplementation project(':lib-adad')
	myketImplementation project(':lib-magnet')
}

apply plugin: 'com.google.gms.google-services'
